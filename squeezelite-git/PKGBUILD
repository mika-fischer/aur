# Maintainer: Mika Fischer <mika.fischer@zoopnet.de>
pkgname=squeezelite-git
pkgver=20130212
pkgrel=1
pkgdesc='Lightweight headless squeezebox emulator'
arch=('i686' 'x86_64' 'armv6h')
url='http://code.google.com/p/squeezelite/'
license=(GPL3)
depends=('alsa-lib')
makedepends=('alsa-lib' 'faad2' 'flac' 'libmad' 'libvorbis' 'mpg123' 'portaudio')
optdepends=('faad2: Support for decoding AAC'
            'flac: Support for decoding FLAC'
            'libmad: Support for decoding MP3'
            'libvorbis: Support for decoding Ogg Vorbis'
            'mpg123: Support for decoding MP3'
            'portaudio: Use of binary using portaudio for output')
provides=('squeezelite')
conflicts=('squeezelite')
source=()
md5sums=()

_gitroot="https://code.google.com/p/squeezelite/"
_gitname="squeezelite"

build() {
    cd "${srcdir}"

    msg "Connecting to GIT server...."
    if [[ -d "${_gitname}" ]]; then
        cd "${_gitname}" && git pull origin master
        msg "The local files are updated."
    else
        git clone "${_gitroot}" "${_gitname}"
    fi
    msg "GIT checkout done or server timeout"

    msg "Starting build..."
    rm -rf "${srcdir}/${_gitname}-build"
    git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
    cd "${srcdir}/${_gitname}-build"

    echo "OPTS += -DPORTAUDIO" > Makefile.archpa
    echo "CFLAGS += -Wall -fPIC -O2 \$(OPTS)" >> Makefile.archpa
    echo "LDFLAGS += -lportaudio -lasound -lpthread -ldl -lrt" >> Makefile.archpa
    echo "EXECUTABLE = squeezelite-pa" >> Makefile.archpa
    echo "include Makefile" >> Makefile.archpa
    make -f Makefile.archpa

    make clean

    echo "CFLAGS += -Wall -fPIC -O2" > Makefile.arch
    echo "LDFLAGS += -lasound -lpthread -ldl -lrt" >> Makefile.arch
    echo "EXECUTABLE = squeezelite" >> Makefile.arch
    echo "include Makefile" >> Makefile.arch
    make -f Makefile.arch
}

package() {
    cd "${srcdir}/${_gitname}-build"

    # Main binary
    install -m755 -D squeezelite    "${pkgdir}/usr/bin/squeezelite"
    install -m755 -D squeezelite-pa "${pkgdir}/usr/bin/squeezelite-pa"
}
