# Maintainer: Mika Fischer <mika.fischer@zoopnet.de>
# Contributor: Gergely Imreh <imrehgATgmailDOTcom>
pkgname=ninja
pkgver=1.0.0
pkgrel=2
pkgdesc='Small build system with a focus on speed'
arch=('i686' 'x86_64')
url='http://martine.github.com/ninja/'
license=(Apache)
depends=('gcc-libs')
makedepends=('asciidoc' 'emacs' 'python2' 're2c')
optdepends=('emacs: for Emacs mode')
install=ninja.install
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/martine/ninja/tarball/v${pkgver}")
md5sums=('53ec431171fbbfa01ca806682f047abc')

_python="python2"

build() {
    cd "${srcdir}/martine-ninja-7096bf1"
    ${_python} bootstrap.py
    emacs -Q --batch -f batch-byte-compile misc/ninja-mode.el
    asciidoc doc/manual.asciidoc
}

package() {
    cd "${srcdir}/martine-ninja-7096bf1"

    # Main binary
    install -m755 -D ninja \
                     "${pkgdir}/usr/bin/ninja"

    # Manual
    install -m644 -D doc/manual.asciidoc \
                     "${pkgdir}/usr/share/doc/ninja/manual.asciidoc"
    install -m644 -D doc/manual.html \
                     "${pkgdir}/usr/share/doc/ninja/manual.html"

    # Emacs mode
    install -m644 -D misc/ninja-mode.el \
                     "${pkgdir}/usr/share/emacs/site-lisp/ninja-mode.el"
    install -m644 -D misc/ninja-mode.elc \
                     "${pkgdir}/usr/share/emacs/site-lisp/ninja-mode.elc"
}
