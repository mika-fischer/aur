# Maintainer: Mika Fischer <mika.fischer@zoopnet.de>
# Contributor: Gergely Imreh <imrehgATgmailDOTcom>
pkgname=ninja
pkgver=1.1.0
pkgrel=1
pkgdesc='Small build system with a focus on speed'
arch=('i686' 'x86_64')
url='http://martine.github.com/ninja/'
license=(Apache)
depends=('gcc-libs')
makedepends=('asciidoc' 'python2' 're2c')
# makedepends+=('emacs')
optdepends=('emacs: for Emacs mode')
install=ninja.install
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/martine/ninja/tarball/v${pkgver}")
md5sums=('c5264c5f8637e0a6eb510af89d101e9e')

_python="python2"

build() {
    cd "${srcdir}/martine-ninja-2c953d1"
    ${_python} bootstrap.py
    if [[ "${makedepends[@]}" =~ "emacs" ]]; then
        emacs -Q --batch -f batch-byte-compile misc/ninja-mode.el
    fi
    asciidoc doc/manual.asciidoc
}

package() {
    cd "${srcdir}/martine-ninja-2c953d1"

    # Main binary
    install -m755 -D ninja \
                     "${pkgdir}/usr/bin/ninja"

    # Manual
    install -m644 -D doc/manual.asciidoc \
                     "${pkgdir}/usr/share/doc/ninja/manual.asciidoc"
    install -m644 -D doc/manual.html \
                     "${pkgdir}/usr/share/doc/ninja/manual.html"

    # Emacs mode
    install -m644 -D misc/ninja-mode.el \
                     "${pkgdir}/usr/share/emacs/site-lisp/ninja-mode.el"
    if [[ "${makedepends[@]}" =~ "emacs" ]]; then
        install -m644 -D misc/ninja-mode.elc \
                         "${pkgdir}/usr/share/emacs/site-lisp/ninja-mode.elc"
    fi
}
